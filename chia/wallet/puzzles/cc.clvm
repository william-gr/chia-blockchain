; Coins locked with this puzzle are spendable ccs.
;
; Choose a list of n inputs (n>=1), I_1, ... I_n with amounts A_1, ... A_n.
;
; We put them in a ring, so "previous" and "next" have intuitive k-1 and k+1 semantics,
; wrapping so {n} and 0 are the same, ie. all indices are mod n.
;
; Each coin creates 0 or more coins with total output value O_k.
; Let D_k = the "debt" O_k - A_k contribution of coin I_k, ie. how much debt this input accumulates.
; Some coins may spend more than they contribute and some may spend less, ie. D_k need
; not be zero. That's okay. It's enough for the total of all D_k in the ring to be 0.
;
; A coin can calculate its own D_k since it can verify A_k (it's hashed into the coin id)
; and it can sum up `CREATE_COIN` conditions for O_k.
;
; Defines a "subtotal of debts" S_k for each coin as follows:
;
; S_1 = 0
; S_k = S_{k-1} + D_{k-1}
;
; Here's the main trick that shows the ring sums to 0.
; You can prove by induction that S_{k+1} = D_1 + D_2 + ... + D_k.
; But it's a ring, so S_{n+1} is also S_1, which is 0. So D_1 + D_2 + ... + D_k = 0.
; So the total debts must be 0, ie. no coins are created or destroyed.
;
; Each coin's solution includes I_{k-1}, I_k, and I_{k+1} along with proofs that each is a CC.
; Each coin's solution includes S_{k-1}. It calculates D_k = O_k - A_k, and then S_k = S_{k-1} + D_{k-1}
;
; Announcements are used to ensure that each S_k follows the pattern is valid.
; Announcements automatically commit to their own coin id.
; Coin I_k creates an announcement that further commits to I_{k-1} and S_{k-1}.
;
; Coin I_k gets a proof that I_{k+1} is a CC, so it knows it must also create an announcement
; when spent. It checks that I_{k+1} creates an announcement committing to I_k and S_k.
;
; So S_{k+1} is correct iff S_k is correct.
;
; Coins also receive proofs that their neighbors are ccs, ensuring the announcements aren't forgeries, as
; inner puzzles are not allowed to use `CREATE_COIN_ANNOUNCEMENT`.
;
; In summary, I_k generates an announcement Y_k (for "yell") as follows:
;
;  Y_k: hash of I_k (automatically), I_{k-1}, S_k
;
; Each coin ensures that the next coin's announcement is as expected:
;  Y_{k+1} : hash of I_{k+1}, I_k, S_{k+1}
;
; TLDR:
;  I_k : coins
;  A_k : amount coin k contributes
;  O_k : amount coin k spend
;  D_k : difference/delta that coin k incurs (A - O)
;  S_k : subtotal of debts D_1 + D_2 ... + D_k
;  Y_k : announcements created by coin k commiting to I_{k-1}, I_k, S_k
;
; All conditions go through a "transformer" that looks for CREATE_COIN conditions
; generated by the inner solution, and wraps the puzzle hash ensuring the output is a cc.
;
; Three output conditions are prepended to the list of conditions for each I_k:
;  (ASSERT_MY_ID I_k) to ensure that the passed in value for I_k is correct
;  (CREATE_COIN_ANNOUNCEMENT I_{k-1} S_k) to create this coin's announcement
;  (ASSERT_COIN_ANNOUNCEMENT hashed_announcement(Y_{k+1})) to ensure the next coin really is next and
;     the relative values of S_k and S_{k+1} are correct
;
; This is all we need to do to ensure ccs exactly balance in the inputs and outputs.
;
; Proof:
;   Consider n, k, I_k values, O_k values, S_k and A_k as above.
;   For the (CREATE_COIN_ANNOUNCEMENT Y_{k+1}) (created by the next coin)
;   and (ASSERT_COIN_ANNOUNCEMENT hashed(Y_{k+1})) to match,
;   we see that I_k can ensure that is has the correct value for S_{k+1}.
;
;   By induction, we see that S_{m+1} = sum(i, 1, m) [O_i - A_i] = sum(i, 1, m) O_i - sum(i, 1, m) A_i
;   So S_{n+1} = sum(i, 1, n) O_i - sum(i, 1, n) A_i. But S_{n+1} is actually S_1 = 0,
;   so thus sum(i, 1, n) O_i = sum (i, 1, n) A_i, ie. output total equals input total.
;
; QUESTION: do we want a secondary puzzle that allows for coins to be spent? This could be good for
;  bleaching coins (sendable to any address), or reclaiming them by a central authority.
;

;; GLOSSARY:
;;  MOD_HASH: this code's sha256 tree hash
;;  GENESIS_COIN_CHECKER: the function that determines if a coin can mint new ccs
;;  INNER_PUZZLE: an independent puzzle protecting the coins. Solutions to this puzzle are expected to
;;              generate `AGG_SIG` conditions and possibly `CREATE_COIN` conditions.
;; ---- items above are curried into the puzzle hash ----
;;  inner-puzzle-solution: the solution to the inner puzzle
;;  prev-coin-bundle: the bundle for previous coin
;;  this-coin-bundle: the bundle for this coin
;;  next-coin-bundle: the bundle for next coin
;;  prev-subtotal: the subtotal between prev-coin and this-coin
;;
;; coin-info: `(parent_id puzzle_hash amount)`. This defines the coin id used with ASSERT_MY_COIN_ID
;; coin-bundle: the cons box `(coin-info . lineage_proof)`
;;
;; and automatically hashed in to the announcement generated with CREATE_COIN_ANNOUNCEMENT.
;;

(mod (MOD_HASH                ;; curried into puzzle
      GENESIS_COIN_CHECKER    ;; curried into puzzle
      INNER_PUZZLE            ;; curried into puzzle
      inner_puzzle_solution   ;; if invalid, INNER_PUZZLE will fail
      prev_coin_bundle        ;; used in this coin's announcement, prev_coin ASSERT_COIN_ANNOUNCEMENT will fail if wrong
      this_coin_bundle        ;; verified with ASSERT_MY_COIN_ID
      next_coin_bundle        ;; used to generate ASSERT_COIN_ANNOUNCEMENT
      prev_subtotal           ;; included in announcement, prev_coin ASSERT_COIN_ANNOUNCEMENT will fail if wrong
    )

     ;;;;; start library code

     (include condition_codes.clvm)
     (include curry-and-treehash.clinc)

     (defmacro assert items
         (if (r items)
             (list if (f items) (c assert (r items)) (q . (x)))
             (f items)
         )
     )

     ; takes a lisp tree and returns the hash of it
     (defun sha256tree1 (TREE)
           (if (l TREE)
               (sha256 2 (sha256tree1 (f TREE)) (sha256tree1 (r TREE)))
               (sha256 1 TREE)))

    ; lineage_proof = (MOD_HASH MOD_HASH_hash GENESIS_COIN_CHECKER GENESIS_COIN_CHECKER_hash)

    (defun-inline mod_hash_from_lineage_proof (lineage_proof) (f lineage_proof))
    (defun-inline mod_hash_hash_from_lineage_proof (lineage_proof) (f (r lineage_proof)))
    (defun-inline genesis_coin_checker_hash_from_lineage_proof (lineage_proof) (f (r (r (r lineage_proof)))))

     ;;;;; end library code

     ;; return the puzzle hash for a cc with the given `GENESIS_COIN_CHECKER_hash` & `INNER_PUZZLE`
     (defun-inline cc_puzzle_hash (lineage_proof inner_puzzle_hash)
       (puzzle-hash-of-curried-function (mod_hash_from_lineage_proof lineage_proof)
                                        inner_puzzle_hash
                                        (genesis_coin_checker_hash_from_lineage_proof lineage_proof)
                                        (mod_hash_hash_from_lineage_proof lineage_proof)
       )
     )

     ;; tweak `CREATE_COIN` condition by wrapping the puzzle hash, forcing it to be a cc
     ;; prohibit CREATE_COIN_ANNOUNCEMENT
     (defun-inline morph_condition (condition lineage_proof_parameters)
       (if (= (f condition) CREATE_COIN)
         (list CREATE_COIN
               (cc_puzzle_hash lineage_proof_parameters (f (r condition)))
               (f (r (r condition)))
         )
         (if (= (f condition) CREATE_COIN_ANNOUNCEMENT)
           (x)
           condition
         )
       )
     )

     ;; tweak all `CREATE_COIN` conditions, enforcing created coins to be ccs
     (defun morph_conditions (conditions lineage_proof_parameters)
       (if conditions
         (c
           (morph_condition (f conditions) lineage_proof_parameters)
           (morph_conditions (r conditions) lineage_proof_parameters)
         )
         ()
       )
     )

     ;; given a coin triplet, return the id of the coin
     (defun-inline coin_id_for_coin (coin)
       (sha256 (f coin) (f (r coin)) (f (r (r coin))))
     )

     ;; utility to fetch coin amount from coin
     (defun-inline input_amount_for_coin (coin)
       (f (r (r coin)))
     )

     ;; calculate the hash of an announcement
     (defun-inline calculate_annoucement_id (this_coin_info this_subtotal next_coin_info)
       ; NOTE: the next line containts a bug, as sha256tree1 ignores `this_subtotal`
       (sha256 (coin_id_for_coin next_coin_info) (sha256tree1 (list this_coin_info this_subtotal)))
     )

     ;; create the `ASSERT_COIN_ANNOUNCEMENT` condition that ensures the next coin's announcement is correct
     (defun-inline create_assert_next_announcement_condition (this_coin_info this_subtotal next_coin_info)
       (list ASSERT_COIN_ANNOUNCEMENT
             (calculate_annoucement_id this_coin_info
                                            this_subtotal
                                            next_coin_info
             )
       )
     )

     ;; here we commit to I_{k-1} and S_k
     (defun-inline create_announcement_condition (prev_coin_info prev_subtotal)
       (list CREATE_COIN_ANNOUNCEMENT
             (sha256tree1 (list prev_coin_info prev_subtotal))
       )
      )

     ;;;;;;;;;;;;;;;;;;;;;;;;;;;

     ;; this function takes a condition and returns an integer indicating
     ;; the value of all output coins created with CREATE_COIN. If it's not
     ;; a CREATE_COIN condition, it returns 0.

     (defun-inline output_value_for_condition (condition)
       (if (= (f condition) CREATE_COIN)
         (f (r (r condition)))
         0
       )
     )

     ;; this function takes a list of conditions and returns an integer indicating
     ;; the value of all output coins created with CREATE_COIN
     (defun output_totals (conditions)
       (if conditions
         (+ (output_value_for_condition (f conditions)) (output_totals (r conditions)))
         0
       )
     )

     ;; ensure `this_coin_info` is correct by creating the `ASSERT_MY_COIN_ID` condition
     (defun-inline create_assert_my_id (this_coin_info)
       (list ASSERT_MY_COIN_ID (coin_id_for_coin this_coin_info))
     )

     ;; add three conditions to the list of morphed conditions:
     ;; ASSERT_MY_COIN_ID for `this_coin_info`
     ;; CREATE_COIN_ANNOUNCEMENT for my announcement
     ;; ASSERT_COIN_ANNOUNCEMENT for the next coin's announcement
     (defun-inline generate_final_output_conditions
       (
         prev_subtotal
         this_subtotal
         morphed_conditions
         prev_coin_info
         this_coin_info
         next_coin_info
       )
       (c (create_assert_my_id this_coin_info)
         (c (create_announcement_condition prev_coin_info prev_subtotal)
           (c (create_assert_next_announcement_condition this_coin_info this_subtotal next_coin_info)
             morphed_conditions)
         )
       )
      )

    (defun-inline coin_info_for_coin_bundle (coin_bundle)
      (f coin_bundle)
    )

     ;;;;;;;;;;;;;;;;;;;;;;;;;;; lineage checking

     ;; return true iff parent of `this_coin_info` is provably a cc
     (defun is_parent_cc (
       lineage_proof_parameters
       this_coin_info
       (parent_parent_coin_id parent_inner_puzzle_hash parent_amount)
     )
       (= (f this_coin_info)
          (sha256 parent_parent_coin_id
                  (cc_puzzle_hash lineage_proof_parameters parent_inner_puzzle_hash)
                  parent_amount
          )
       )
     )

     ;; return true iff the lineage proof is valid
     ;; lineage_proof is of one of two forms:
     ;;  (1 . (parent_parent_coin_id parent_inner_puzzle_hash parent_amount))
     ;;  (0 . some_opaque_proof_passed_to_GENESIS_COIN_CHECKER)
     ;; so the `f` value determines what kind of proof it is, and the `r` value is the proof

     (defun-inline genesis_coin_checker_for_lpp (lineage_proof_parameters)
       (f (r (r lineage_proof_parameters)))
     )

     (defun-inline is_lineage_proof_valid (lineage_proof_parameters coin_info lineage_proof)
       (if
         (f lineage_proof)
         (is_parent_cc lineage_proof_parameters coin_info (r lineage_proof))
         (a (genesis_coin_checker_for_lpp lineage_proof_parameters)
             (list lineage_proof_parameters coin_info (r lineage_proof)))
       )
     )

     (defun is_bundle_valid ((coin . lineage_proof) lineage_proof_parameters)
       (is_lineage_proof_valid lineage_proof_parameters coin lineage_proof)
     )

     (defun merge_list (one two)
      (if one
        (c (f one) (merge_list (r one) two))
        two
      )
     )

     (defun if_is_list_merge (inspected_item output)
       (if (l inspected_item)
           (merge_list inspected_item output)
           output
        )
     )


     ;;;;;;;;;;;;;;;;;;;;;;;;;;;

     (defun main (
         lineage_proof_parameters
         inner_conditions
         prev_coin_bundle
         this_coin_bundle
         next_coin_bundle
         prev_subtotal
      )
      (if_is_list_merge
        ; ensure prev is a cc (is this really necessary?)
        (is_bundle_valid prev_coin_bundle lineage_proof_parameters)
        (if_is_list_merge
          ; ensure this is a cc (to ensure parent wasn't counterfeit)
          (is_bundle_valid this_coin_bundle lineage_proof_parameters)
          (if_is_list_merge
            ; ensure next is a cc (to ensure its announcements can be trusted)
            (is_bundle_valid next_coin_bundle lineage_proof_parameters)

            (generate_final_output_conditions
              prev_subtotal
              ; the expression on the next line calculates `this_subtotal` by adding the delta to `prev_subtotal`
              (+ prev_subtotal (- (input_amount_for_coin (coin_info_for_coin_bundle this_coin_bundle)) (output_totals inner_conditions)))
              (morph_conditions inner_conditions lineage_proof_parameters)
              (coin_info_for_coin_bundle prev_coin_bundle)
              (coin_info_for_coin_bundle this_coin_bundle)
              (coin_info_for_coin_bundle next_coin_bundle)
            )
          )
        )
      )
    )

    (main
        ;; cache some stuff: output conditions, and lineage_proof_parameters
        (list MOD_HASH (sha256tree1 MOD_HASH) GENESIS_COIN_CHECKER (sha256tree1 GENESIS_COIN_CHECKER))
        (a INNER_PUZZLE inner_puzzle_solution)
        prev_coin_bundle
        this_coin_bundle
        next_coin_bundle
        prev_subtotal
    )
)
