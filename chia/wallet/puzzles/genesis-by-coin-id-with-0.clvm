; This is a "genesis checker" for use with cc.clvm.
;
; This checker allows new CATs to be created if they have a particular coin id as parent
;
; The genesis_id is curried in, making this lineage_check program unique and giving the CAT it's uniqueness

(mod (
      GENESIS_ID
      Truths ; These should be the same CAT truths, but minus the opaque solution which gets passed below
      delta
      _
    )
    ; COLOURED_COIN TRUTHS ARE: my_id full_puzzle_hash inner_puzzle_hash my_amount my_parent CC_STRUCT
    (defun-inline access_parent_id_from_truths (Truths) (f (r (r (r (r Truths))))))

    ; Returns nil since we don't need to add any conditions
    (if delta
        (x)
        (if (= (access_parent_id_from_truths Truths) GENESIS_ID)
            ()
            (x "parent_info" (access_parent_id_from_truths Truths))
        )
    )

)
